plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
}

android {
    namespace "com.shubham.srmpyq"
    compileSdkVersion localProperties.getProperty('flutter.compileSdkVersion')?.toInteger() ?: 34
    ndkVersion localProperties.getProperty('flutter.ndkVersion') ?: "25.1.8937393"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    defaultConfig {
        applicationId "com.shubham.srmpyq"
        minSdkVersion localProperties.getProperty('flutter.minSdkVersion')?.toInteger() ?: 23
        targetSdkVersion localProperties.getProperty('flutter.targetSdkVersion')?.toInteger() ?: 34
        versionCode 9// Increment this value with each release
        versionName "9.0"
    }

    // Load the keystore file for signing (only if it exists)
    def keystorePropertiesFile = rootProject.file("key.properties")
    def keystoreProperties = new Properties()
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }

    signingConfigs {
        release {
            storeFile keystoreProperties["storeFile"] ? file(keystoreProperties["storeFile"]) : null
            storePassword keystoreProperties["storePassword"]
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}
dependencies {
    implementation 'com.google.android.gms:play-services-auth:20.7.0'  // Ensure this version is present
}
flutter {
    source "../.."
}
